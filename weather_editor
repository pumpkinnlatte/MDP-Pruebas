% --- 1. Definiciones de Dominio ---

% Declaraci贸n de Fluentes y Acciones
1/3::clima(lluvioso, 0); 1/3::clima(nublado, 0); 1/3::clima(soleado, 0).
0.5::viento(0).

esperar.

% --- 2. Modelo de Transici贸n para 'clima' (t -> t+1) ---

% Caso: Soleado + Sin Viento
0.8::clima(soleado, 1); 0.15::clima(nublado, 1); 0.05::clima(lluvioso, 1) :-
    clima(soleado, 0), not(viento(0)), esperar.

% Caso: Soleado + Con Viento
0.6::clima(soleado, 1); 0.3::clima(nublado, 1); 0.1::clima(lluvioso, 1) :-
    clima(soleado, 0), viento(0),esperar.

% Caso: Nublado + Sin Viento
0.3::clima(soleado, 1); 0.4::clima(nublado, 1); 0.3::clima(lluvioso, 1) :-
    clima(nublado, 0),not(viento(0)),esperar.

% Caso: Nublado + Con Viento
0.5::clima(soleado, 1); 0.2::clima(nublado, 1); 0.3::clima(lluvioso, 1) :-
    clima(nublado, 0), viento(0),esperar.

% Caso: Lluvioso + Sin Viento
0.1::clima(soleado, 1); 0.2::clima(nublado, 1); 0.7::clima(lluvioso, 1) :-
    clima(lluvioso, 0), not(viento(0)),esperar.

% Caso: Lluvioso + Con Viento
0.2::clima(soleado, 1); 0.4::clima(nublado, 1); 0.4::clima(lluvioso, 1) :-
    clima(lluvioso, 0),viento(0),esperar.

% --- 3. Modelo de Transici贸n para 'viento' (t -> t+1) ---

% Si hay viento, tiende a seguir habiendo viento (0.7)
0.7::viento(1) :-
    viento(0),esperar.

% Aparici贸n: Si no hay viento, puede levantarse (0.2)
0.2::viento(1) :-
    not(viento(0)),esperar.
    

evidence(clima(lluvioso, 0), true).
evidence(viento(0), true).

query(clima(lluvioso, 1)).
query(clima(nublado, 1)).
query(clima(soleado, 1)).

query(clima(lluvioso, 0)).
query(clima(nublado, 0)).
query(clima(soleado, 0)).

query(viento(1)).